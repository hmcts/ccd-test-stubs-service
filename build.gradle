plugins {
  id 'application'
  id 'jacoco'
  id 'io.spring.dependency-management' version '1.0.10.RELEASE'
  id 'org.springframework.boot' version '2.7.18'
  id 'com.github.ben-manes.versions' version '0.20.0'
  id 'org.sonarqube' version '4.4.0.3356'
  id 'uk.gov.hmcts.java' version '0.12.40'
}

group = 'uk.gov.hmcts.reform.ccd'

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(17)
  }
}

sourceSets {
  integrationTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/integrationTest/java')
    }
    resources.srcDir file('src/integrationTest/resources')
  }

  smokeTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/smokeTest/java')
    }
    resources.srcDir file('src/smokeTest/resources')
  }

  functionalTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/functionalTest/java')
    }
    resources.srcDir file('src/functionalTest/resources')
  }
}

tasks.withType(JavaCompile) {
  options.compilerArgs << "-Xlint:unchecked" << "-Werror"
}

tasks.withType(Test) {
  useJUnitPlatform()

  testLogging {
    exceptionFormat = 'full'
  }
}

test {
  failFast = true
}

task integration(type: Test, description: 'Runs the integration tests.', group: 'Verification') {
  testClassesDirs = sourceSets.integrationTest.output.classesDirs
  classpath = sourceSets.integrationTest.runtimeClasspath
  useJUnitPlatform{
    includeTags 'integration'
  }
  // set your environment variables here
  environment("WIREMOCK_SERVER_MAPPINGS_PATH", "wiremock")
}

task smoke(type: Test) {
  testClassesDirs = sourceSets.smokeTest.output.classesDirs
  classpath = sourceSets.smokeTest.runtimeClasspath
  useJUnitPlatform {
    includeTags 'smoke'
  }
}

task functional(type: Test) {
  testClassesDirs = sourceSets.functionalTest.output.classesDirs
  classpath = sourceSets.functionalTest.runtimeClasspath
  useJUnitPlatform {
    includeTags 'functional'
  }
}

jacocoTestReport {
  executionData(test, integration)
  reports {
    xml.enabled = true
    csv.enabled = false
    xml.destination file("${buildDir}/reports/jacoco/test/jacocoTestReport.xml")
  }
}

check.dependsOn integration

project.tasks['sonarqube'].dependsOn test, integration, jacocoTestReport

sonarqube {
  properties {
    property "sonar.projectName", "Reform :: ccd-test-stubs-service"
    property "sonar.projectKey", "uk.gov.hmcts.reform:ccd-test-stubs-service"
    property "sonar.jacoco.reportPaths", "${project.buildDir}/jacoco/test.exec"
    property "sonar.jacoco.itReportPath", "${project.buildDir}/jacoco/integration.exec"
    property "sonar.junit.reportPaths", "${project.buildDir}/test-results/junit-platform"
    property "sonar.coverage.jacoco.xmlReportPaths", "${jacocoTestReport.reports.xml.destination.path}"
  }
}

dependencyUpdates.resolutionStrategy = {
  componentSelection { rules ->
    rules.all { ComponentSelection selection ->
      boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
        selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
      }
      if (rejected) {
        selection.reject('Release candidate')
      }
    }
  }
}

apply from: './gradle/suppress.gradle'
dependencyCheck {
  suppressionFile = 'dependency-check-suppressions.xml'
}

repositories {
  mavenLocal()
  mavenCentral()
  maven {
    url 'https://jitpack.io'
  }
}

ext {
  groovyVersion = '3.0.7'
  tomcatVersion = '9.0.83'
  jettyVersion = '9.4.53.v20231009'
}

ext['spring-framework.version'] = '5.3.27'
ext['spring-security.version'] = '5.7.10'
ext['log4j2.version'] = '2.17.1'
ext['jackson.version'] = '2.14.1'
ext['snakeyaml.version'] = '2.0'

// it is important to specify logback classic and core packages explicitly as libraries like spring boot
// enforces it's own (older) version which is not recommended.
def versions = [
  junit           : '5.3.2',
  reformLogging   : '6.0.1',
  springBoot      : '3.0',
  springfoxSwagger: '3.0.0',
]


dependencies {
  implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-bootstrap', version: '3.1.8'
  implementation group: 'com.google.guava', name: 'guava', version:'32.1.2-jre'
  implementation group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.13'

  implementation "org.apache.tomcat.embed:tomcat-embed-core:${tomcatVersion}"
  implementation "org.apache.tomcat.embed:tomcat-embed-el:${tomcatVersion}"
  implementation "org.apache.tomcat.embed:tomcat-embed-websocket:${tomcatVersion}"

  // CVE-2021-28170
  implementation group: 'org.glassfish', name: 'jakarta.el', version: '4.0.1'
  // CVE-2021-42550
  implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.13'
  implementation group: 'ch.qos.logback', name: 'logback-core', version: '1.2.13'

  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-aop'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-json'

  implementation group: 'com.github.hmcts.java-logging', name: 'logging', version: versions.reformLogging
  implementation(group: 'org.springframework.cloud', name: 'spring-cloud-starter-netflix-hystrix', version: '2.2.10.RELEASE') {
    exclude group: 'org.springframework.security', module: 'spring-security-rsa'
  }

  // https://mvnrepository.com/artifact/com.github.tomakehurst/wiremock
  implementation(group: 'com.github.tomakehurst', name: 'wiremock-jre8', version: '2.35.0')
// https://mvnrepository.com/artifact/com.github.jknack/handlebars
  implementation group: 'com.github.jknack', name: 'handlebars', version: '4.3.1'

  implementation group: 'org.eclipse.jetty', name: 'jetty-server', version: jettyVersion
  implementation group: 'org.eclipse.jetty', name: 'jetty-alpn-client', version: jettyVersion
  implementation group: 'org.eclipse.jetty', name: 'jetty-alpn-java-client', version: jettyVersion
  implementation group: 'org.eclipse.jetty', name: 'jetty-alpn-java-server', version: jettyVersion
  implementation group: 'org.eclipse.jetty', name: 'jetty-alpn-openjdk8-client', version: jettyVersion
  implementation group: 'org.eclipse.jetty', name: 'jetty-alpn-openjdk8-server', version: jettyVersion
  implementation group: 'org.eclipse.jetty', name: 'jetty-continuation', version: jettyVersion
  implementation group: 'org.eclipse.jetty', name: 'jetty-util', version: jettyVersion
  implementation group: 'org.eclipse.jetty', name: 'jetty-util-ajax', version: jettyVersion
  implementation group: 'org.eclipse.jetty', name: 'jetty-xml', version: jettyVersion
  implementation group: 'org.eclipse.jetty', name: 'jetty-client', version: jettyVersion
  implementation group: 'org.eclipse.jetty', name: 'jetty-security', version: jettyVersion
  implementation group: 'org.eclipse.jetty', name: 'jetty-http', version: jettyVersion
  implementation group: 'org.eclipse.jetty', name: 'jetty-io', version: jettyVersion
  implementation group: 'org.eclipse.jetty.http2', name: 'http2-server', version: jettyVersion
  implementation group: 'org.eclipse.jetty.http2', name: 'http2-common', version: jettyVersion
  implementation group: 'org.eclipse.jetty.http2', name: 'http2-hpack', version: jettyVersion

  implementation group: 'org.eclipse.jetty', name: 'jetty-servlet', version: jettyVersion
  implementation group: 'org.eclipse.jetty', name: 'jetty-servlets', version: jettyVersion
  implementation group: 'org.eclipse.jetty', name: 'jetty-webapp', version: jettyVersion
  implementation group: 'org.eclipse.jetty', name: 'jetty-proxy', version: jettyVersion
  implementation group: 'org.eclipse.jetty', name: 'jetty-alpn-server', version: jettyVersion
  implementation group: 'org.eclipse.jetty', name: 'jetty-alpn-conscrypt-server', version: jettyVersion
  implementation group: 'org.eclipse.jetty', name: 'jetty-alpn-conscrypt-client', version: jettyVersion

  implementation group: 'org.codehaus.groovy', name: 'groovy-json', version: groovyVersion
  implementation group: 'org.codehaus.groovy', name: 'groovy', version: groovyVersion
  implementation group: 'org.codehaus.groovy', name: 'groovy-xml', version: groovyVersion

  implementation group: 'com.nimbusds', name: 'nimbus-jose-jwt', version: '7.9'
  implementation group: 'org.springframework.security', name: 'spring-security-crypto'


  implementation group: 'commons-fileupload', name: 'commons-fileupload', version: '1.5'
  implementation group: 'commons-beanutils', name: 'commons-beanutils', version: '1.9.4'
  implementation group: 'commons-io', name: 'commons-io', version: '2.8.0'

  testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: versions.junit
  testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: versions.junit
  testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: versions.junit

  testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test'
  testImplementation(group: 'io.rest-assured', name: 'rest-assured', version: '4.3.0') {
    exclude group: 'org.apache.sling', module: 'org.apache.sling.javax.activation'
  }
  testImplementation group: 'io.rest-assured', name: 'json-path', version: '4.3.0'
  testImplementation(group: 'io.rest-assured', name: 'xml-path', version: '4.3.0') {
    exclude group: 'org.apache.sling', module: 'org.apache.sling.javax.activation'
  }

  testImplementation group: 'org.hamcrest', name: 'hamcrest', version: '2.2'
  testImplementation 'com.github.hmcts:fortify-client:1.3.0:all'
  integrationTestImplementation sourceSets.main.runtimeClasspath
  integrationTestImplementation sourceSets.test.runtimeClasspath

  smokeTestImplementation sourceSets.main.runtimeClasspath
  smokeTestImplementation sourceSets.test.runtimeClasspath

  functionalTestImplementation sourceSets.main.runtimeClasspath
  functionalTestImplementation sourceSets.test.runtimeClasspath

  implementation group: 'junit', name: 'junit', version: '4.13.1'

  implementation group: 'net.minidev', name: 'json-smart', version: '2.4.7'

  // CCD-3511 CVE-2020-15522
  implementation group: 'org.bouncycastle', name: 'bcpkix-jdk15to18', version: '1.77'

  implementation 'org.springframework.cloud:spring-cloud-starter-bootstrap:3.1.8'
  implementation('org.springframework.cloud:spring-cloud-starter'){
    version {
      strictly '3.1.8'
    }
  }

  implementation('org.springframework.boot:spring-boot-starter'){
    version {
      strictly '3.1.8'
    }
  }

  implementation('org.springframework.security:spring-security-rsa'){
    version {
      strictly '1.0.12.RELEASE'
    }
  }

  implementation('org.bouncycastle:bcpkix-jdk18on') {
    version {
      strictly '1.77'
    }
  }
}

test {
  useJUnitPlatform()
  failFast = true
}

mainClassName = 'uk.gov.hmcts.reform.ccd.test.stubs.service.Application'

bootJar {
  archiveName = 'ccd-test-stubs-service.jar'

  manifest {
    attributes('Implementation-Title': project.name)
    attributes('Implementation-Version': project.version.toString())
  }
  from('wiremock/mappings') {
    into 'wiremock/mappings'
  }
}

dependencyManagement {
  imports {
    mavenBom "org.springframework.cloud:spring-cloud-dependencies:2021.0.1"
  }
}

// for cnp pipeline
task dependencyCheck(dependsOn: dependencyCheckAnalyze)

task fortifyScan(type: JavaExec)  {
  main = "uk.gov.hmcts.fortifyclient.FortifyClientMainApp"
  classpath += sourceSets.test.runtimeClasspath
  jvmArgs = ['--add-opens=java.base/java.lang.reflect=ALL-UNNAMED']
  // The line below prevents the build from failing if the Fortify scan detects issues
  ignoreExitValue = true
}
